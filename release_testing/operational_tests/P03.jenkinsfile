// P03 performs end-to-end test synchronizing from the first block to the current head on chosen network

pipeline {
    agent { node "x86-4-32-m" }

    options {
        timestamps ()
        timeout(time: 2, unit: 'DAYS')
        disableConcurrentBuilds(abortPrevious: false)
    }

    environment {
        GOGC = '50'
        GOMEMLIMIT = '28GiB'
        DATAROOTPATH="/mnt/tmp-disk/tooltmp"
        DATADIRPATH="${DATAROOTPATH}/sonic"
        GENESISPATH = "${DATAROOTPATH}/genesis"
        TOMLPATH = "${DATAROOTPATH}/sonic-config.toml"

        // genesis download url
        GENESISURL = ''
        TOMLURL = ''
        GENESISFILE = 'genesis.json'

        // client additional options
        OPTIONS = ''
    }

    parameters {
        string(
            name: 'SonicVersion',
            defaultValue: "develop",
            description: 'Branch or commit hash for Sonic'
        )
        choice(
            name: 'Network',
            choices: ['Sonic mainnet', 'Blaze testnet'],
            description: 'Network to test'
        )
    }

    stages {
        stage('Checkout and build') {
            steps {
                checkout scmGit(
                    branches: [[name: "${SonicVersion}"]],
                    userRemoteConfigs: [[
                        url: 'https://github.com/Fantom-foundation/Sonic.git'
                    ]]
                )

                sh "make"
            }
        }

        // This stage sets genesis download url for the chosen network,
        // and also download the configuration file if needed.
        // Set addtional options for the network here.
        stage('Configure Network Parameters') {
            steps{
                script {
                    if ("${Network}" == "Sonic mainnet") {
                        echo 'Target network: Sonic mainnet'
                        GENESISURL = 'https://storage.googleapis.com/sonic-snapshots/sonic-mainnet/sonic.g'
                        TOMLURL = 'https://storage.googleapis.com/sonic-snapshots/sonic-mainnet/config.toml'
                        OPTIONS = "--lachesis.suppress-frame-panic --config ${TOMLPATH}"
                        GENESISFILE = 'sonic.g'
                    } else if ("${Network}" == "Blaze testnet") {
                        echo 'Target network: Blaze testnet'
                        GENESISURL = 'https://storage.googleapis.com/sonic-snapshots/blaze-testnet/blaze.g'
                        TOMLURL = 'https://storage.googleapis.com/sonic-snapshots/blaze-testnet/config.toml'
                        OPTIONS = "--config ${TOMLPATH}"
                        GENESISFILE = 'blaze.g'
                    }
                }
            }
        }

        stage('Genesis download and verification') {
            steps {
                // setup
                sh "mkdir -p $GENESISPATH"

                // download genesis
                script {
                    sh "wget -O ${GENESISPATH}/${GENESISFILE} ${GENESISURL} -nv"
                    sh "wget -O ${TOMLPATH} ${TOMLURL} -nv"

                    // download MD5
                    sh "wget -O ${GENESISPATH}/${GENESISFILE}.md5 ${GENESISURL}.md5 -nv"
                    def md5Expected = sh(script: "cat ${GENESISPATH}/${GENESISFILE}.md5", returnStdout: true);

                    String md5ExpectedHash = md5Expected.substring(0, md5Expected.lastIndexOf(' '));

                    // calculate MD5
                    def genesisMd5 = sh(script: "md5sum ${GENESISPATH}/${GENESISFILE}", returnStdout: true);

                    String md5CalculatedHash = genesisMd5.substring(0, genesisMd5.lastIndexOf(' '));

                    sh "echo ${md5ExpectedHash}"
                    sh "echo ${md5CalculatedHash}"

                    // compare MD5
                    if (md5ExpectedHash.trim() != md5CalculatedHash.trim()) {
                        sh "echo removing corrupted file ${GENESISPATH}/${GENESISFILE}"
                        sh "rm ${GENESISPATH}/${GENESISFILE}"
                        currentBuild.result = 'FAILURE'
                        error("Error hash mismatch: got ${md5CalculatedHash}, expected got ${md5ExpectedHash}")
                    } else {
                        sh "echo hash ${md5CalculatedHash} confirmed"
                    }
                }
            }
        }

        stage('Create DB from genesis') {
           steps {
               // create db from a genesis file
                sh "./build/sonictool --datadir ${DATADIRPATH} genesis --experimental ${GENESISPATH}/${GENESISFILE}"
                sh "./build/sonictool --datadir ${DATADIRPATH} check live"
            }
        }

        stage('Synchronization and check') {
            steps {
                sh "./build/sonicd --datadir ${DATADIRPATH} --verbosity 2 --exitwhensynced.age 1s ${OPTIONS}"
                sh "./build/sonictool --datadir ${DATADIRPATH} check live"
            }
        }

        stage('Teardown') {
            steps {
                sh "make clean"
                sh "rm -rf ${DATAROOTPATH}"
            }
        }
    }

    post {
        always {
            build job: '/Notifications/slack-notification', parameters: [
                string(name: 'result', value: "${currentBuild.result}"),
                string(name: 'name', value: "${currentBuild.fullDisplayName}"),
                string(name: 'duration', value: "${currentBuild.duration}"),
                string(name: 'url', value: "${currentBuild.absoluteUrl}"),
                string(name: 'user', value: "sonic")
            ]
        }
    }
}
