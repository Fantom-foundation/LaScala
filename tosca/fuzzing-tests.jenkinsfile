// Runs Tosca Fuzzing Tests
pipeline {
    agent { label 'fuzzing' }

    options {
        timestamps()
        // This is a global timeout for the whole pipeline,
        // it is a failsave to prevent the pipeline from running if missonfigured.
        // This timeout will set the pipeline to ABORTED when triggered
        timeout(time: 6, unit: 'HOURS')
        disableConcurrentBuilds(abortPrevious: false)
    }

    environment {
        GOMEMLIMIT = '30GiB'
        GOCACHE = '/mnt/tmp-disk/go-cache'
    }

    parameters {
        string(
            name: 'ToscaVersion',
            defaultValue: 'main',
            description: 'Can be either branch name or commit hash.'
        )
        choice(
            name: 'EntryPoint',
            choices: ['FuzzLfvm', 'FuzzGeth', 'FuzzDifferentialLfvmVsGeth'],
            description: 'Selects which fuzzer test function to start.'
        )

        booleanParam(
            name: 'CleanCache',
            defaultValue: false, description: 'clean cache before running'
        )
    }

    stages {
        stage('clean-cache') {
            when {
                expression { return params.CleanCache }
            }
            steps {
                script {
                    sh "rm -rf $GOCACHE"
                }
            }
        }

        stage('checkout') {
            steps {
                script {
                    currentBuild.description = "Building on ${env.NODE_NAME}"
                }

                checkout scmGit(
                    branches: [[name: params.ToscaVersion]],
                    userRemoteConfigs: [[
                        url: 'https://github.com/Fantom-foundation/Tosca.git'
                    ]]
                )
                sh 'git submodule update --init --recursive --depth 1'
            }
        }

        stage('build-and-test') {
            steps {
                // Unit tests are run to ensure that the code is working as expected.
                sh 'make test'
            }
        }

        stage('fuzzing-test') {
            steps {
                // Fuzzing time is set to 5 hours. Notice that pipeline timeout
                // is larger to accommodate for checkout, build, and test stages.
                sh "go test -fuzz=${EntryPoint} ./go/ct -fuzztime 5h"
            }

            post {
                failure {
                    // Archive the faulty inputs found to be downloaded and analyzed.
                    archiveArtifacts artifacts: "go/ct/testdata/fuzz/${EntryPoint}/*"
                }
            }
        }
    }

    post {
        always {
            // Send a slack notification
            build job: '/Notifications/slack-notification', parameters: [
                string(name: 'result', value: "${currentBuild.result}"),
                string(name: 'name', value: "${currentBuild.fullDisplayName}"),
                string(name: 'duration', value: "${currentBuild.duration}"),
                string(name: 'url', value: "${currentBuild.absoluteUrl}"),
                string(name: 'user', value: 'tosca')
            ]
        }
    }
}
